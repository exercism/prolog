% Wordy Module -- Built for SWI-PL v8.0.3

:- module(wordy, [wordy/2]).

:- use_module(library(dcg/basics)).

% wordy(+Question, -Answer)
%
wordy(Question, Answer) :-
  string_codes(Question, CQuestion),
  phrase(parse(Ts), CQuestion),
  !,
  compute(Ts, Answer).

compute([T | Ts], Answer) :- compute(Ts, T, Answer).

compute([], Answer, Answer).

compute(["+", T | Ts], Acc, Answer) :-
  Next is Acc + T,
  compute(Ts, Next, Answer).

compute(["-", T | Ts], Acc, Answer) :-
  Next is Acc - T,
  compute(Ts, Next, Answer).

compute(["*", T | Ts], Acc, Answer) :-
  Next is Acc * T,
  compute(Ts, Next, Answer).

compute(["/", T | Ts], Acc, Answer) :-
  Next is div(Acc, T),
  compute(Ts, Next, Answer).

% Wordy DCG

parse(E) -->
  `What is`,
  math_expr(E, number),
  `?`.

parse(_) -->
  { throw(error(unknown_operation_error, wrong_form)) }.

math_expr(E, T) --> blank, math_expr(E, T).

math_expr([I | Es], number) -->
  integer(I),
  math_expr(Es, operator).

math_expr(_, number) -->
  { throw(error(syntax_error, integer_expected)) }.

math_expr([], operator) --> [].

math_expr([O | Es], operator) -->
  operator(O),
  math_expr(Es, number).

math_expr(_, operator) -->
  { throw(error(unknown_operation_error, operator_not_implemented)) }.

operator("+") --> `plus`.
operator("-") --> `minus`.
operator("*") --> `multiplied by`.
operator("/") --> `divided by`.
operator(I)   -->
  integer(I),
  { throw(error(syntax_error, integer_found_rather_than_operator)) }.